apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'
apply plugin: PlaygroundPlugin

buildProperties {

    secrets {
        using(project.file('secrets.properties')).or([
                apiKey   : 'N/A',
                apiSecret: 'N/A',
                username : 'N/A',
                password : 'N/A'
        ])
        description = """
                |This file should contain the following properties from a valid Letterboxd account:
                |  apiKey: the Letterboxd account API key
                |  apiSecret: the Letterboxd account API secret
                |  username: the Letterboxd account username
                |  password: the Letterboxd account password
                |""".stripMargin()
    }
}

android {
    applySdk()

    defaultConfig {
        buildConfigString 'API_KEY', buildProperties.secrets['apiKey']
        buildConfigString 'API_SECRET', buildProperties.secrets['apiSecret']
        buildConfigString 'USERNAME', buildProperties.secrets['username']
        buildConfigString 'PASSWORD', buildProperties.secrets['password']
    }
}

dependencies {
    api project(':features:common:api')

    implementation G.maven.kotlin.stdlib

    testImplementation G.maven.junit
    testImplementation G.maven.truth
}

jacoco {
    exclude '**/*Companion.class', '**/*Json.class'
}
